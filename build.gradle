buildscript {

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:+'
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

allprojects {

    group = 'com.github.wtekiela'
    version = '0.1.3-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'license'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'idea'

    sourceCompatibility = 1.8

    license {
        header rootProject.file('LICENSE')
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
        ext.name = 'Wojciech Tekiela'

        include "**/*.java"
        exclude "**/*Test.java"
    }

    repositories {
        mavenCentral()
    }

    test {
        useTestNG()

        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }

        // listen to standard out and standard error of the test JVM(s)
        onOutput { descriptor, event ->
            logger.lifecycle(event.message)
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
       // sign configurations.archives
    }

    idea {
        module {
            //if you prefer different output folders
            inheritOutputDirs = false
            outputDir = file('build')
            testOutputDir = file('build')

            downloadJavadoc = true
            downloadSources = true
        }
    }

}
